defmodule Badge.App.Sokoban.Page do
  @moduledoc """
  Sokoban: push every box onto a goal.

  Arrows move and push, `r` restarts the level. Enter opens level select:
  left and right pick any unlocked level, Enter plays it, Esc goes back to
  the board. A solved level shows "Solved!" for a second, then the next one
  opens. Esc on the board goes home.

  The highest unlocked level is loaded on the first tick and saved in NVS
  under `sokoban` whenever it grows.
  """

  use Badge.Page

  alias Badge.App.Sokoban.Board
  alias Badge.App.Sokoban.Levels
  alias Badge.FontType
  alias Badge.Nvs
  alias Badge.Readout
  alias Badge.Theme

  @nvs_key :sokoban
  @count Levels.count()
  @top Theme.content_top()
  @bar_y 216
  @area_h @bar_y - @top - 2
  @max_tile 24
  @pause_ms 1000
  @margin 8

  @impl true
  def title, do: "Sokoban"

  @impl true
  def init,
    do:
      :maps.merge(
        %{mode: :play, unlocked: 1, saved: 1, loaded: false, pick: 1, until: 0},
        level(1)
      )

  @impl true
  def handle_key({:move, dir}, %{mode: :play} = state) do
    case Board.move(state.board, dir) do
      {:blocked, _board} -> {:ok, state}
      {_moved, board} -> {:ok, solve(%{state | board: board, moves: state.moves + 1})}
    end
  end

  def handle_key({:char, c}, %{mode: :play} = state) when c == ?r or c == ?R,
    do: {:ok, %{state | board: state.start, moves: 0}}

  def handle_key({:edit, :newline}, %{mode: :play} = state),
    do: {:ok, %{state | mode: :select, pick: state.level}}

  def handle_key({:move, :left}, %{mode: :select} = state),
    do: {:ok, %{state | pick: max(state.pick - 1, 1)}}

  def handle_key({:move, :right}, %{mode: :select} = state),
    do: {:ok, %{state | pick: min(state.pick + 1, state.unlocked)}}

  def handle_key({:edit, :newline}, %{mode: :select} = state), do: {:ok, play(state, state.pick)}
  def handle_key({:nav, :home}, %{mode: :select} = state), do: {:ok, %{state | mode: :play}}
  def handle_key({:edit, :newline}, %{mode: :done} = state), do: {:ok, play(state, 1)}
  def handle_key(_event, _state), do: :ignore

  @impl true
  def tick(state), do: state |> load() |> advance() |> persist()

  @impl true
  def leave(state) do
    persist(state)

    :ok
  end

  @doc false
  def advance(%{mode: :solved} = state) do
    cond do
      now() < state.until -> state
      state.level >= @count -> %{state | mode: :done}
      true -> play(state, state.level + 1)
    end
  end

  def advance(state), do: state

  @doc false
  def decode(<<n>>) when n >= 1 and n <= @count, do: n
  def decode(_value), do: 1

  defp load(%{loaded: true} = state), do: state

  defp load(state) do
    unlocked = decode(Nvs.get(@nvs_key))

    play(%{state | unlocked: unlocked, saved: unlocked, loaded: true}, unlocked)
  end

  defp persist(%{unlocked: n, saved: n} = state), do: state

  defp persist(state) do
    Nvs.put(@nvs_key, <<state.unlocked>>)

    %{state | saved: state.unlocked}
  end

  defp play(state, n), do: :maps.merge(%{state | mode: :play}, level(n))

  defp level(n) do
    board = Board.parse(Levels.get(n))
    tile = min(@max_tile, min(div(Theme.width(), board.w), div(@area_h, board.h)))

    %{
      level: n,
      board: board,
      start: board,
      moves: 0,
      runs: Board.runs(board),
      tile: tile,
      x0: div(Theme.width() - board.w * tile, 2),
      y0: @top + div(@area_h - board.h * tile, 2)
    }
  end

  defp solve(state) do
    case Board.solved?(state.board) do
      true ->
        %{
          state
          | mode: :solved,
            until: now() + @pause_ms,
            unlocked: min(max(state.unlocked, state.level + 1), @count)
        }

      false ->
        state
    end
  end

  defp now, do: :erlang.monotonic_time(:millisecond)

  @impl true
  def render(%{mode: :select} = state),
    do: centred("< Level " <> int(state.pick) <> " >") ++ bar(state)

  def render(state),
    do:
      banner(state.mode) ++
        player(state) ++ boxes(state) ++ goals(state) ++ walls(state) ++ bar(state)

  defp banner(:solved), do: centred("Solved!")
  defp banner(:done), do: centred("All solved")
  defp banner(_mode), do: []

  defp player(%{board: %{player: {x, y}}} = state),
    do: [cell(state, x, y, div(state.tile, 4), Theme.accent())]

  defp boxes(state) do
    for {x, y} = box <- state.board.boxes do
      cell(
        state,
        x,
        y,
        2,
        if(:lists.member(box, state.board.goals), do: Theme.ok(), else: Theme.warn())
      )
    end
  end

  defp goals(state),
    do: for({x, y} <- state.board.goals, do: cell(state, x, y, div(state.tile * 3, 8), Theme.dim()))

  defp walls(state) do
    for {x, y, n} <- state.runs,
        do: {:rect, state.x0 + x * state.tile, state.y0 + y * state.tile, n * state.tile, state.tile, Theme.muted()}
  end

  defp cell(state, x, y, inset, colour) do
    {:rect, state.x0 + x * state.tile + inset, state.y0 + y * state.tile + inset, state.tile - 2 * inset, state.tile - 2 * inset, colour}
  end

  defp centred(text),
    do: [
      {:text, Readout.centre_x(text), @top + div(@area_h, 2) - 8, FontType.body(), Theme.fg(), Theme.bg(), text}
    ]

  defp bar(state) do
    font = FontType.heading()
    where = "Level " <> int(state.level) <> "/" <> int(@count) <> "  Moves " <> int(state.moves)

    Theme.rule(0, @bar_y, Theme.width()) ++
      [
        {:text, @margin, @bar_y + 2, font, Theme.dim(), Theme.bg(), hints(state.mode)},
        {:text, Readout.right_x(where, font), @bar_y + 2, font, Theme.fg(), Theme.bg(), where}
      ]
  end

  defp hints(:select), do: "left/right pick  Enter play"
  defp hints(:done), do: "Enter play again"
  defp hints(_mode), do: "r restart  Enter levels"

  defp int(n), do: :erlang.integer_to_binary(n)
end
